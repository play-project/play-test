#
# Real-time and historic pattern detecting 4 company-related events where one poster has previously posted in the past.
#

PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX uctelco: <http://events.event-processing.org/uc/telco/>
PREFIX geo:     <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX :        <http://events.event-processing.org/types/>

CONSTRUCT {
	:e rdf:type :UcTelcoEsrRecom .
	:e :stream <http://streams.event-processing.org/ids/OverallResults03#stream> .
	:e uctelco:ackRequired "false"^^xsd:boolean .
	:e uctelco:answerRequired "false"^^xsd:boolean .
	:e :message "Pattern 03: Four company-related events were detected where one poster has previously posted in the past."^^xsd:string .
	:e uctelco:action <blank://action1> .
	<blank://action1> rdf:type uctelco:OpenTwitter ;
		:screenName ?p03screenName01 , ?p03screenName02 , ?p03screenName03 , ?p03screenName04 .
	:e :members ?p03e1 , ?p03e2 , ?p03e3 , ?p03e4 .
}
WHERE {
	WINDOW {
		EVENT ?p03id1 {
			?p03e1 rdf:type :google .
			?p03e1 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?p03e1 :screenName ?p03screenName01 .
			}
		SEQ
		EVENT ?p03id2 {
			?p03e2 rdf:type :apple .
			?p03e2 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?p03e2 :screenName ?p03screenName02 .
			}
		SEQ
		EVENT ?p03id3 {
			?p03e3 rdf:type :microsoft .
			?p03e3 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?p03e3 :screenName ?p03screenName03 .
			}
		SEQ
		EVENT ?p03id4 {
			?p03e4 rdf:type :yahoo .
			?p03e4 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?p03e4 :screenName ?p03screenName04 .
			}
	} ("PT5S"^^xsd:duration, sliding)
	GRAPH ?p03id5 {
		?p03e5 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
		?p03e5 :screenName ?p03screenName01 .
	}
}
