#
# Selective pattern detecting 4 company-related events in sequence bounded by a time window
# and filtering twitter events for number of friends and more.
#

PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX uctelco: <http://events.event-processing.org/uc/telco/>
PREFIX geo:     <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX :        <http://events.event-processing.org/types/>

CONSTRUCT {
	:e rdf:type :UcTelcoEsrRecom .
	:e :stream <http://streams.event-processing.org/ids/OverallResults02#stream> .
	:e uctelco:ackRequired "false"^^xsd:boolean .
	:e uctelco:answerRequired "false"^^xsd:boolean .
	:e :message "Pattern 02: Four company-related events were detected using stricter filters."^^xsd:string .
	:e uctelco:action <blank://action1> .
	<blank://action1> rdf:type uctelco:OpenTwitter ;
		:screenName ?screenName01 , ?screenName02 , ?screenName03 , ?screenName04 .
	:e :members ?e1 , ?e2 , ?e3 , ?e4 .
}
WHERE {
	WINDOW {
		EVENT ?id1 {
			?e1 rdf:type :google .
			?e1 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?e1 :screenName ?screenName01 .
			?e1 :isRetweet "false" .
			?e1 :friendsCount ?friendsCount01 .
			}
			FILTER(?friendsCount01 > "10")
		SEQ
		EVENT ?id2 {
			?e2 rdf:type :apple .
			?e2 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?e2 :screenName ?screenName02 .
			?e2 :isRetweet "false" .
			?e2 :friendsCount ?friendsCount02 .
			}
			FILTER(?friendsCount02 > "10")
		SEQ
		EVENT ?id3 {
			?e3 rdf:type :microsoft .
			?e3 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?e3 :screenName ?screenName03 .
			?e3 :isRetweet "false" .
			?e3 :friendsCount ?friendsCount03 .
			}
			FILTER(?friendsCount03 > "10")
		SEQ
		EVENT ?id4 {
			?e4 rdf:type :yahoo .
			?e4 :stream <http://streams.event-processing.org/ids/TwitterFeed#stream> .
			?e4 :screenName ?screenName04 .
			?e4 :isRetweet "false" .
			?e4 :friendsCount ?friendsCount04 .
			}
			FILTER(?friendsCount04 > "10")
	} ("PT5S"^^xsd:duration, sliding)
}
